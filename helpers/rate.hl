
/*
 * File responsible for allowing user to "rate" an object.
 */





/*
 * Creating our "rate" widget.
 */
create-widget:harvester-container
  class:container
  widgets
    div
      class:row
      widgets
        div
          class:col-100
          widgets
            div:widget-wrp
              class:shaded rounded air-inner bg air-top
              widgets
                h2
                  innerValue:What do you think about Phosphorus Five?
                  class:center
                harvester.widgets.star-rating:harvester-rater
                  class:center harvester-star-rater
                literal:harvester-comment
                  element:textarea
                  class:fill
                  rows:5
                  placeholder:Comment ...
                div:rate-wrp
                  class:right
                  widgets
                    button
                      innerValue:Rate
                      onclick

                        /*
                         * Retrieving rating, and verifying it's more than zero.
                         */
                        harvester.widgets.star-rating.value:harvester-rater
                        if:x:/@harvester.widgets.star-rating.value?value
                          =:int:0

                          /*
                           * No rating was supplied.
                           */
                          micro.windows.info:Please click star 1-5 to supply your rating!
                          return

                        /*
                         * Retrieving comment, and storing it in database with star rating value.
                         *
                         * Making sure we store the ID for the database record for the user
                         * in the viewstate, such that we can later attach his email address
                         * with the object.
                         */
                        get-widget-property:harvester-comment
                          value
                        p5.mysql.connect:[camphora]
                          p5.mysql.insert:@"insert into `star-rater` (`rating`, `comment`) values (@rating, @comment)"
                            @rating:x:/@harvester.widgets.star-rating.value?value
                            @comment:x:/@get-widget-property/*/*?value
                          p5.web.viewstate.set:harvester.db-id
                            src:x:/@p5.mysql.insert/*/id?value

                        /*
                         * Hiding stars and textarea, and displays "type in email" address.
                         */
                        set-widget-property:harvester-rater
                          visible:false
                        set-widget-property:harvester-comment
                          visible:false
                        set-widget-property:rate-wrp
                          visible:false

                        /*
                         * Invoking file responsible for asking user about his email address.
                         */
                        micro.evaluate.file:@HARVESTER/helpers/harvest-email.hl
