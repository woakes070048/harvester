
/*
 * File responsible for allowing user to "rate" an object.
 */





/*
 * Creating our "harvester" widget, trying to collect user's email address.
 */
create-widget
  parent:widget-wrp
  class:strip fill col-60 offset-20
  widgets
    input:collect-name
      placeholder:Full name ...
      type:text
      onkeydown:@"if (event.keyCode == 13) {p5.$('submit-email').raise('onclick');return false;}"
      oninit

        /*
         * Settings initial focus to this bugger - (name textbox).
         */
        micro.page.set-focus:x:/../*/_event?value

    input:collect-email
      placeholder:Email address ...
      type:text
      onkeydown:@"if (event.keyCode == 13) {p5.$('submit-email').raise('onclick');return false;}"
    button:submit-email
      innerValue:Submit
      onclick

        /*
         * Retrieving name/email user typed in.
         */
        get-widget-property:collect-email
          value
        get-widget-property:collect-name
          value

        /*
         * Retrieving the database ID for the current rating, and updating it with
         * an actual email address now.
         */
        p5.web.viewstate.get:harvester.db-id
        p5.mysql.connect:[camphora]
          p5.mysql.update:@"update `star-rater` set `email` = @email, `name` = @name where id = @id"
            @email:x:/../*/get-widget-property/*/collect-email/*?value
            @name:x:/../*/get-widget-property/*/collect-name/*?value
            @id:x:/@p5.web.viewstate.get/*?value
          p5.mysql.scalar:@"select `rating` from `star-rater` where id = @id"
            @id:x:/@p5.web.viewstate.get/*?value

        /*
         * Disabling the button, to avoid double clicks.
         */
        set-widget-property:submit-email
          disabled

        /*
         * Invoking file responsible for sending the auto-responder email.
         */
        eval-x:x:/+/*
        micro.evaluate.file:@HARVESTER/helpers/send-auto-responder.hl
          id:x:/@p5.web.viewstate.get/*?value

        /*
         * Re-directing user to "profile page" after 1 second, but only if he is
         * at least "partially happy" about the object.
         */
        if:x:/@p5.mysql.connect/*/p5.mysql.scalar?value.int
          >:int:2

          /*
           * User was happy about this, hence we'll re-direct him to his "profile page",
           * allowing him to share it on Facebook.
           */
          p5.web.set-location:/harvester/profile/{0}
            :x:/@p5.web.viewstate.get/*?value

        else

          /*
           * User was not happy about this, letting him know somebody will contact him about
           * his experience, and try to fix his issues.
           */
          create-widgets
            micro.widgets.modal
              widgets
                h3
                  innerValue:I am sorry to hear that you were not happy
                p
                  innerValue:@"An associate from GaiaSoul will contact you soon, to hear if we can somehow help you, to make sure your experience with Phosphorus Five becomes more pleasent."





/*
 * Creating our "disclaimer, 'opt in'" widget, informing user we will solicit him.
 */
create-widget
  element:p
  parent:widget-wrp
  class:col-60 offset-20
  style:"color:#bbbbbb;"
  innerValue:@"By supplying your email address in the above field, and clicking submit, you accept that we can send you emails about Phosphorus Five and the GaiaSoul Suite."
